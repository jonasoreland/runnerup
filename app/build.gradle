apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'

android {
    compileSdkVersion 20
    buildToolsVersion '20.0.0'

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        froyo {
            manifest.srcFile 'froyo/AndroidManifest.xml'
            java.srcDirs = ['froyo/java']
        }
        latest {
            manifest.srcFile 'latest/AndroidManifest.xml'
            java.srcDirs = ['latest/java']
        }
    }

    productFlavors {
        froyo {
            minSdkVersion 8
            targetSdkVersion 18
            versionName "1.37"
            versionCode 37 
        }
        latest {
            minSdkVersion 14
            targetSdkVersion 18
            versionName "1.37"
            versionCode 37
        }
    }

    signingConfigs {
        release {
            storeFile file("../../my-release-key.keystore")
            keyAlias "alias_name"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    latestCompile 'com.android.support:support-v4:20.0.0'
    latestCompile 'com.google.android.gms:play-services:6.1.11'

    froyoCompile 'com.android.support:support-v4:19.1.+'
    froyoCompile 'com.google.android.gms:play-services:3.2.65'

    compile files('../GraphView/public/graphview-3.1.jar')
    provided files('libs/samsung_ble_sdk_200.jar')
    compile files('libs/antpluginlib_2.0.0.jar')

    compile project(':common')
}

allprojects {
    tasks.withType(JavaCompile) {
        // options.compilerArgs << "-Xlint:deprecation";
        options.compilerArgs << "-Xlint:unchecked"
    }
}

task downloadSamsungBleSdk(type: DownloadTask) {
    sourceUrl = 'https://github.com/fishkingsin/BLEDialogTool/raw/master/lib/samsung_ble_sdk_200.jar'
    target = file('libs/samsung_ble_sdk_200.jar')
}
preBuild.dependsOn downloadSamsungBleSdk

task downloadAntPluginLib(type: DownloadTask) {
    sourceUrl = 'https://github.com/ant-wireless/Android_API_ANTPlus/raw/master/antpluginlib_2.0.0.jar'
    target = file('libs/antpluginlib_2.0.0.jar')
}
preBuild.dependsOn downloadAntPluginLib

class DownloadTask extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}

def props = new Properties()
if (rootProject.file("../release.properties").exists()) {
    props.load(new FileInputStream(rootProject.file("../release.properties")))
    android.signingConfigs.release.storePassword props.keyStorePassword
    android.signingConfigs.release.keyPassword props.keyAliasPassword
}

