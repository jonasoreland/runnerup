apply plugin: 'com.android.application'

import java.util.regex.Pattern

def doExtractStringFromManifest(name) {
    def manifestFile = file(android.sourceSets.main.manifest.srcFile)
    def pattern = Pattern.compile(name + "=\"(.*?)\"")
    def matcher = pattern.matcher(manifestFile.getText())
    matcher.find()
    return matcher.group(1)
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        froyo {
            manifest.srcFile 'froyo/AndroidManifest.xml'
            java.srcDirs = ['froyo/java']
        }
        latest {
            manifest.srcFile 'latest/AndroidManifest.xml'
            java.srcDirs = ['latest/java']
            assets.srcDirs = ['latest/assets']
        }
        test.setRoot('test')
    }

    productFlavors {
        latest {
            minSdkVersion 15
            targetSdkVersion rootProject.ext.compileSdkVersion
            versionName rootProject.ext.versionName
            versionCode rootProject.ext.versionCode
        }
        froyo {
            minSdkVersion 8
            targetSdkVersion rootProject.ext.targetSdkVersion
            versionName rootProject.ext.versionName
            versionCode rootProject.ext.froyoVersionCode
        }
    }

    defaultConfig {
        applicationId = doExtractStringFromManifest("package")
        //By default all AppCompat translations are included, saves 350KB
        resConfigs "ar", "bs", "cs", "ca", "de", "en", "es", "fr", "hu", "id", "it", "ja", "lt", "nb", "nl", "pl", "pt", "ru", "sv", "tr"
    }

    signingConfigs {
        //noinspection GroovyMissingReturnStatement
        release {
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        //disable specific checks for specific paths
        lintConfig file("lint.xml")
        checkReleaseBuilds true
        //promote to error, must be handled
        fatal 'InlinedApi', 'InconsistentArrays'
        //Lint check cannot handle that there are different versions
        //TODO: Find how this should be handled
        //check manually with ./gradlew app:dependencies
        warning 'GradleCompatible'
        //Support lib > 25.0.1 _sometimes_ raises this issue. Should be fixed though
        warning 'RestrictedApi'
        //All translations will not be complete
        ignore 'MissingTranslation'
        //icon with all densities are not included - dont bother
        ignore 'IconMissingDensityFolder'
        //apply() is supported from SDK 9, but min version is 8
        ignore 'ApplySharedPref'

        showAll true
        //debug issues in Travis
        textReport true
        textOutput 'stdout'
    }
}

repositories {
    //jcenter and mavenCentral normally have the same packages but there may be a delay after updating the primary
    jcenter() //Google
    mavenCentral() //MapBox GraphView
    maven { url "https://oss.sonatype.org/content/groups/public/" } //pebblekit
}

dependencies {
    latestCompile "com.android.support:design:${rootProject.ext.supportLibrary}"
    froyoCompile 'com.android.support:design:24.1.0' //SDK <10 dropped in 24.2
    latestCompile "com.google.android.gms:play-services-wearable:${rootProject.ext.googlePlayServicesVersion}"
    latestCompile 'com.getpebble:pebblekit:4.0.1'
    latestCompile ('com.mapbox.mapboxsdk:mapbox-android-sdk:4.2.0@aar'){
        transitive=true
    }
    //Adding GraphView twice is done to force two inclusions and 'upgrade' the version of
    //Android support library that GraphView was compiled with twice
    //(GraphView is not used in 2.2 (but 2.3 "Froyo" builds). A wrapper will maybe suppress for Froyo.)
    latestCompile 'com.jjoe64:graphview:4.2.1'
    froyoCompile 'com.jjoe64:graphview:4.2.1'
    compile project(':common')
    compile project(':hrdevice')
    latestWearApp project(':wear')

    testCompile "junit:junit:${rootProject.ext.junitVersion}"
    testCompile "org.mockito:mockito-core:${rootProject.ext.mockitoVersion}"
}

allprojects {
    tasks.withType(JavaCompile) {
        // options.compilerArgs << "-Xlint:deprecation";
        options.compilerArgs << "-Xlint:unchecked"
    }
}

def props = new Properties()
if (rootProject.file("release.properties").exists()) {
    props.load(new FileInputStream(rootProject.file("release.properties")))

    android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
    android.signingConfigs.release.storePassword props.keyStorePassword
    android.signingConfigs.release.keyAlias props.keyAlias
    android.signingConfigs.release.keyPassword props.keyAliasPassword
} else {
    project.logger.info('INFO: Set the values storeFile, storePassword, keyAlias, and keyPassword in release.properties to sign the release.')
    android.buildTypes.release.signingConfig = null
}

android.applicationVariants.all { variant ->
    variant.mergeResources.doLast {
        if (rootProject.file("mapbox.properties").exists()) {
            props.load(new FileInputStream(rootProject.file("mapbox.properties")))
            File valuesFile = file("${buildDir}/intermediates/res/merged/${variant.dirName}/values/values.xml")
            String content = valuesFile.getText('UTF-8')
            content = content.replaceAll(/mapboxMapID.*/, "mapboxMapID\">" + props.mapboxid + "</string>")
            content = content.replaceAll(/mapboxAccessToken.*/, "mapboxAccessToken\">" + props.mapboxAccessToken + "</string>")
            valuesFile.write(content, 'UTF-8')
        }
    }
}
