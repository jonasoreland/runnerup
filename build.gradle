buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.0'
    }
}

repositories {
    google()
}

project.ext {
    //Common settings for all builds
    //Note that Android Studio does not know about the 'ext' module and will warn
    //minSdkVersion differs between modules
    buildToolsVersion = '33.0.1' //Update Travis manually
    compileSdkVersion = 33 //Update Travis manually
    targetSdkVersion = 33

    appcompat_version = "1.6.0"
    annotation_version = "1.5.0"
    //Note: Later Play Services will require a rewrite of NodeApi.NodeListener
    googlePlayServicesVersion = '11.0.4'
    googleWearVersion = '2.3.0'

    junitVersion = '4.13.2'
    mockitoVersion = '3.11.2'

    //The Git tag for the release must be identical for F-Droid
    versionName = '2.5.0.0'
    versionCode = 320
    latestBaseVersionCode = 15000000

    // F-Droid builds only allow free software (wear dir deleted at builds)
    allowNonFree = !project.hasProperty('org.runnerup.free') && rootProject.file("wear").exists()
    enableWear = allowNonFree

    // Note: AntPlus may have to be downloaded explicitly due to licensing
    // Therefore, the .aar file may not be redistributed in the RU repo
    antPlusLibPath = "$rootDir/ANT-Android-SDKs/ANT+_Android_SDK/API"
    antPlusLibName = "antpluginlib_3-8-0"
    if (!allowNonFree || project.hasProperty('org.runnerup.hr.disableAntPlus') || !rootProject.file(antPlusLibPath+'/'+antPlusLibName+'.aar').exists()) {
        antPlusLib = ""
    }

    // ApplicationId (possibly with a .debug suffix) shared Android and Wear
    // Separate Id allows simultaneous installation
    if (!rootProject.ext.allowNonFree) {
        // Separate applicationId for the free version (must be first)
        applicationId = "org.runnerup.free"
    } else {
        applicationId = "org.runnerup"
    }
}

// Special setup for antPlusLib, not allowed to copy the .aar and create mavenLocal
// Raises warning:
// AGPBI: {"kind":"warning","text":"Using flatDir should be avoided because it doesn't support any meta-data formats.","sources":[{}]}
allprojects {
    repositories {
        if (rootProject.ext.antPlusLibPath) {
            flatDir { dirs rootProject.ext.antPlusLibPath }
        }
    }
}
