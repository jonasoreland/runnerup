buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
    }
}

repositories {
    google()
}

project.ext {
    //Common settings for all builds
    //Note that Android Studio does not know about the 'ext' module and will warn
    //minSdkVersion differs between modules
    buildToolsVersion = '30.0.3' //Update Travis manually
    compileSdkVersion = 30 //Update Travis manually
    targetSdkVersion = 30

    appcompat_version = "1.3.1"
    annotation_version = "1.3.0"
    //Note: Later Play Services will require a rewrite of NodeApi.NodeListener
    googlePlayServicesVersion = '11.0.4'
    googleWearVersion = '2.3.0'

    junitVersion = '4.13.2'
    mockitoVersion = '3.11.2'

    //The Git tag for the release must be identical for F-Droid
    versionName = '2.4.3.0'
    versionCode = 309
    latestBaseVersionCode = 15000000

    travisBuild = System.getenv("TRAVIS") == "true"
    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true" == System.getProperty("pre-dex", "true")

    // F-Droid builds only allow free software (wear dir deleted at builds)
    allowNonFree = !project.hasProperty('org.runnerup.free') && rootProject.file("wear").exists()
    enableWear = allowNonFree

    // Note: AntPlus may have to be downloaded explicitly due to licensing
    antPlusLib = "ANT-Android-SDKs/ANT+_Android_SDK/API/antpluginlib_3-8-0.aar"
    if (!allowNonFree || project.hasProperty('org.runnerup.hr.disableAntPlus') || !rootProject.file(antPlusLib).exists()) {
        antPlusLib = ""
    }

    // ApplicationId (possibly with a .debug suffix) shared Android and Wear
    // Separate Id allows simultaneous installation
    if (!rootProject.ext.allowNonFree) {
        // Separate applicationId for the free version (must be first)
        applicationId = "org.runnerup.free"
    } else {
        applicationId = "org.runnerup"
    }
}
