apply plugin: 'com.android.library'

group = "org.runnerup.hr"
version = "1.0"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion rootProject.ext.targetSdkVersion

        if (rootProject.ext.antPlusLib) {
            buildConfigField 'Boolean', 'ANTPLUS_ENABLED', "true"
        } else {
            android.sourceSets.main.java.excludes = ['org/runnerup/hr/AntPlus.java']
            buildConfigField 'Boolean', 'ANTPLUS_ENABLED', "false"
        }
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation "androidx.appcompat:appcompat:${rootProject.ext.appcompat_version}"

    if (rootProject.ext.antPlusLib) {
        compileOnly files("../" + antPlusLib)
    }
}