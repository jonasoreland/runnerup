apply plugin: 'com.android.library'

group = "org.runnerup.hr"
version = "1.0"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName = version

        if (rootProject.ext.antPlusLib) {
            buildConfigField 'Boolean', 'ANTPLUS_ENABLED', "true"
        } else {
            android.sourceSets.main.java.excludes = ['org/runnerup/hr/AntPlus.java']
            buildConfigField 'Boolean', 'ANTPLUS_ENABLED', "false"
        }
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    if (rootProject.ext.antPlusLib) {
        implementation files("../" + antPlusLib)
    }
}