apply plugin: 'com.android.library'

group = "org.runnerup.hr"
version = "1.0"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            if (project.hasProperty('org.runnerup.hr.disableSamsungBLE')) {
                java.excludes = ['org/runnerup/hr/SamsungBLEHRProvider.java']
            }
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
    defaultConfig {
        minSdkVersion 8
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode 1
        versionName = version
    }
}

dependencies {
    if (!project.hasProperty('org.runnerup.hr.disableSamsungBLE')) {
        provided files('libs/samsung_ble_sdk_200.jar')
    }
    compile files('../ANT-Android-SDKs/ANT+_Android_SDK/API/antpluginlib_3-6-0.jar')
}

task downloadSamsungBleSdk(type: DownloadTask) {
    sourceUrl = 'https://github.com/fishkingsin/BLEDialogTool/raw/master/lib/samsung_ble_sdk_200.jar'
    target = file('libs/samsung_ble_sdk_200.jar')
}
if (!project.hasProperty('org.runnerup.hr.disableSamsungBLE')) {
    preBuild.dependsOn downloadSamsungBleSdk
}

class DownloadTask extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}
